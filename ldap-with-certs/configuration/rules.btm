RULE broker start
CLASS org.apache.activemq.broker.BrokerService
METHOD start
AT ENTRY
IF true
DO traceOpen("myfile", "data/log/byteman.log")
ENDRULE

RULE broker stop
CLASS org.apache.activemq.broker.BrokerService
METHOD stop
AT ENTRY
IF true
DO traceClose("myfile")
ENDRULE

RULE ActiveMQ operation invoked
CLASS com.sun.jmx.mbeanserver.JmxMBeanServer
METHOD invoke
BIND
  objectName:ObjectName = $1;
  operationName:String = $2;
  params:Object[] = $3;
  signature:String[] = $4;
  domain:String = objectName.getDomain();
AT ENTRY
IF domain == "org.apache.activemq"
DO traceln("myfile", "AUDIT | " + objectName + " | " + operationName + " | params: " + java.util.Arrays.toString(params) + " | signature: " + java.util.Arrays.toString(signature));
ENDRULE

RULE ActiveMQ audit invoked
CLASS org.apache.activemq.broker.util.DefaultAuditLog
METHOD log
BIND
  logEntry:AuditLogEntry = $1;
AT ENTRY
IF true
DO traceln("myfile", "AUDIT | " + logEntry.getUser());
ENDRULE


